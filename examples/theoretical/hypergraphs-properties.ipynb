{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "83f7db28",
   "metadata": {},
   "source": [
    "# Generating hypergraphs with some desired properties\n",
    "\n",
    "Here we show how to use HyperGraphs.jl to generate hypergraphs that match certain desired properties.\n",
    "\n",
    "Note that in the examples below we use `-1` a number of times; this is because `0` is a valid degree and cardinality value, meaning that we have to offset iteration counts to align with the first object being index `0` and not `1` as usual."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "59d3fb97",
   "metadata": {},
   "outputs": [],
   "source": [
    "using HyperGraphs"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7f7808b0",
   "metadata": {},
   "source": [
    "## 1. Generating hypergraphs according to some degree distribution\n",
    "\n",
    "The standard approach is to place stubs on each vertex corresponding to the desired number of hyperedges to be incident on that vertex; this effectively fixes the degree distribution, and hyperedges may then be generated by randomly connecting vertices with respect to the stubs.\n",
    "\n",
    "We first show how to do this in the simple case where we have some target degree counts (i.e. how many vertices of each degree should appear in the hypergraph), and where hyperedges are randomly populated with a random number of vertices. We then show how to extend this approach in a number of ways, by for instance drawing degree counts or hyperedge cardinality from a distribution.\n",
    "\n",
    "### 1.1 Generating hypergraph given some degree counts\n",
    "\n",
    "#### 1.1.1. Setting the desired degree counts\n",
    "\n",
    "We start by setting the desired degree counts. The parameters are\n",
    "- `n` the number of vertices,\n",
    "- `target_d_c` the target degree counts."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "1035feec",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(6, [0, 3, 2, 1])"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "n, target_d_c = 6, [0, 3, 2, 1]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c9a97a6a",
   "metadata": {},
   "source": [
    "This means we are asking for `0` vertex with degree `0`, `3` vertices with degree `1`, `2` vertices with degree `2`, and `1` vertex with degree `3`.\n",
    "\n",
    "Note that in this setting we cannot ask for a total degree count that is greater than `n`, i.e. the following must be true:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "84931b38",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "sum(target_d_c) <= n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2f12b65b",
   "metadata": {},
   "source": [
    "We can then get the target degree sequence `target_d_s` from the target degree counts."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "75c3aab0",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "6-element Vector{Int64}:\n",
       " 3\n",
       " 2\n",
       " 2\n",
       " 1\n",
       " 1\n",
       " 1"
      ]
     },
     "execution_count": 4,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "target_d_s = degree_sequence(target_d_c)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bedc16f1",
   "metadata": {},
   "source": [
    "#### 1.1.2. Assigning stubs to vertices\n",
    "\n",
    "We then assign a given number of stubs to each vertex, which effectively fixes the degree distribution. Here we do so by simply assigning degrees to vertices in a random way. In this case, stubs are just given by a map that sends elements of the vertex set `V` to the degree of each element.\n",
    "\n",
    "First we get the vertex set `V`; assuming vertices are integers, we simply do"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "92642c87",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "1:6"
      ]
     },
     "execution_count": 5,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "V = 1:n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c8e8b309",
   "metadata": {},
   "source": [
    "We then create a temporary variable `ds` that holds the target degree sequence; this is needed because we must keep track of which stubs have been used when generating hyperedges, and we do not want to modify the original variable."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "78827bf4",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "6-element Vector{Int64}:\n",
       " 3\n",
       " 2\n",
       " 2\n",
       " 1\n",
       " 1\n",
       " 1"
      ]
     },
     "execution_count": 6,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ds = deepcopy(target_d_s)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "05c626b0",
   "metadata": {},
   "source": [
    "We then assign stubs i.e. make each vertex in `V` correspond to some degree in `ds`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "0675725e",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "([1, 2, 3, 4, 5, 6], [1, 1, 2, 2, 3, 1])"
      ]
     },
     "execution_count": 7,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Random\n",
    "vs, ds = collect(V), Random.shuffle(ds)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4a7ed486",
   "metadata": {},
   "source": [
    "#### 1.1.3 Generating hyperedges\n",
    "\n",
    "Here hyperedges of random cardinality are randomly drawn given available stubs."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "030fd068",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "HyperGraph{Int64}([2, 5, 1, 4, 6, 3], HyperEdge{Int64}[HyperEdge{Int64}([2, 5, 1, 4, 5, 6, 5, 3]), HyperEdge{Int64}([3, 4])])"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "es = Vector{HyperEdge{Int64}}()    # this will hold hyperedges\n",
    "\n",
    "idx = ds .!== 0                    # this keeps track of which vertices have free stubs\n",
    "\n",
    "while any(idx .!= 0)               # hyperedges are generated until all stubs are used\n",
    "    k = rand(1:sum(ds))            # hyperedge cardinality is randomly drawn from the number of available stubs\n",
    "    chosen_vs = Vector{Int64}()    # this will hold the vertices chosen to populate this hyperedge\n",
    "    for _ in 1:k                   # we iterate over k to choose k vertices\n",
    "        chosen_v = rand(vs[idx])   # a vertex is randomly chosen from vertices with free stubs\n",
    "        push!(chosen_vs, chosen_v) # this vertex is added to the vertices that will populate this hyperedge\n",
    "        ds[chosen_v] -= 1          # we decrement the number of stubs assigned to the chosen vertex\n",
    "        idx = ds .!== 0            # one stub has been used up and we need to update idx\n",
    "    end\n",
    "    push!(es, HyperEdge(chosen_vs))\n",
    "end\n",
    "\n",
    "X = HyperGraph(es)                 # we build a hypergraph from the generated hyperedges"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f25ae336",
   "metadata": {},
   "source": [
    "We can now check that the degree sequence of the generated hypergraph matches the target degree sequence."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "b0d302ec",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 9,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "degree_sequence(X) == target_d_s"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f8b8cf1d",
   "metadata": {},
   "source": [
    "### 1.2 Extensions\n",
    "\n",
    "#### 1.2.1. Drawing the degree sequence from a distribution\n",
    "\n",
    "The desired degree counts in `target_d_c` may be drawn from any distribution. Here we demonstrate how to do this by building a distribution from the target degree counts themselves; this is mainly to show how to build a custom `Distribution` type using the Distributions.jl package, but any suitable built-in distribution will work.\n",
    "\n",
    "We first build the target degree distribution from the target degree counts."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "26d3c9c3",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4-element Vector{Float64}:\n",
       " 0.0\n",
       " 0.5\n",
       " 0.3333333333333333\n",
       " 0.16666666666666666"
      ]
     },
     "execution_count": 10,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "target_d_d = target_d_c ./ n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6c158ed0",
   "metadata": {},
   "source": [
    "We can check that the resulting vector is a probability vector."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "85532843",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 11,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using Distributions\n",
    "isprobvec(target_d_d)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "06758131",
   "metadata": {},
   "source": [
    "We can then use that vector to build a custom distribution."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "8f8f6301",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 12,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "_support = 0:length(target_d_c)-1\n",
    "d = DiscreteNonParametric(_support, target_d_d)\n",
    "d isa Distribution"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f89b3dfd",
   "metadata": {},
   "source": [
    "The point of building a custom distribution here is being able to use functions defined on distributions. For example, we can now use `rand` on `d`, which we need to sample a new target degree sequence from it."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "d05e60cf",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "6-element Vector{Int64}:\n",
       " 3\n",
       " 2\n",
       " 2\n",
       " 1\n",
       " 1\n",
       " 1"
      ]
     },
     "execution_count": 13,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "sampled_target_d_s = sort(rand(d, n), rev=true)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ae05adfd",
   "metadata": {},
   "source": [
    "A hypergraph may then be built from this new degree sequence using the same code as above."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "56887a69",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 14,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "ds = deepcopy(sampled_target_d_s)\n",
    "vs, ds = collect(V), Random.shuffle(ds)\n",
    "\n",
    "es = Vector{HyperEdge{Int64}}()\n",
    "\n",
    "idx = ds .!== 0\n",
    "while any(idx .!= 0)\n",
    "    k = rand(1:sum(ds))\n",
    "    chosen_vs = Vector{Int64}()\n",
    "    for _ in 1:k\n",
    "        chosen_v = rand(vs[idx])\n",
    "        push!(chosen_vs, chosen_v)\n",
    "        ds[chosen_v] -= 1\n",
    "        idx = ds .!== 0\n",
    "    end\n",
    "    push!(es, HyperEdge(chosen_vs))\n",
    "end\n",
    "\n",
    "X = HyperGraph(es)\n",
    "\n",
    "degree_sequence(X) == sampled_target_d_s"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "327a765d",
   "metadata": {},
   "source": [
    "#### 1.2.2. Drawing hyperedge cardinalities from a distribution\n",
    "\n",
    "The cardinality of each hyperedge may also be drawn from any distribution; here we illustrate this using uniform and binomial distributions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "19f2640b",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(2, 1)"
      ]
     },
     "execution_count": 15,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "k_range = 0:maximum(target_d_s) # these are the possible cardinality values\n",
    "d1 = DiscreteUniform(first(k_range), last(k_range))\n",
    "d2 = Binomial(last(k_range))\n",
    "rand(d1), rand(d2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c209ab18",
   "metadata": {},
   "source": [
    "The line `k = rand(1:sum(ds))` in the code above just needs to be replaced with `k = rand(d)` where `d` is the distribution of choice."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b9b6926b",
   "metadata": {},
   "source": [
    "#### 1.2.3. Restricting hyperedge vertex sets to be sets and not multisets\n",
    "\n",
    "In the code above, there is no restriction on vertex multiplicity in the hyperedge vertex set. The resulting hypergraphs still satisfy the required degree sequence because the notion of degree we implement in HyperGraphs.jl generalises the one that is commonly used (see [this section](https://github.com/lpmdiaz/HyperGraphs.jl/blob/main/README.md#properties) of the HyperGraphs.jl README for a short discussion of degree). One may however not want the hyperedge vertex set to be a multiset, thus satisfying the definition of the degree of some vertex _v_ as _the cardinality of the set of hyperedges incident on that vertex_.\n",
    "\n",
    "We show below how to achieve this. Our approach relies on the `sample` function from StatsBase.jl. This also shortens the code since `k` vertices may be drawn at once."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "fae9b983",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 16,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "using StatsBase\n",
    "\n",
    "target_d_s = degree_sequence(target_d_c)\n",
    "ds = deepcopy(target_d_s)\n",
    "vs, ds = (collect(V), Random.shuffle(ds))\n",
    "\n",
    "es = Vector{HyperEdge{Int64}}()\n",
    "\n",
    "idx = ds .!== 0\n",
    "while any(idx .!= 0)\n",
    "    k = rand(1:sum(idx))\n",
    "    chosen_vs = sample(vs[idx], k, replace=false)\n",
    "    [ds[chosen_v] -= 1 for chosen_v in chosen_vs]\n",
    "    idx = ds .!== 0\n",
    "    push!(es, HyperEdge(chosen_vs))\n",
    "end\n",
    "\n",
    "X = HyperGraph(es)\n",
    "\n",
    "degree_sequence(X) == target_d_s"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fefb2d48",
   "metadata": {},
   "source": [
    "Note that this approach sets a lower bound on the number of hyperedges, according to"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "ad70d72b",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "3"
      ]
     },
     "execution_count": 17,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "min_n_es = length(target_d_c) - 1"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "679478bf",
   "metadata": {},
   "source": [
    "#### 1.2.4. Dealing with target degree counts not summing up to `n`\n",
    "\n",
    "Whenever the target degree counts do not sum up to `n`, degree information is is missing: some vertices will have no assigned target degree. We suggest dealing with that case by simply assuming the missing values should be degree `0`. We start in the same way as before, and check that we are missing some information:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "dfafc2ec",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "false"
      ]
     },
     "execution_count": 18,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "target_d_c = [0, 3, 2]\n",
    "sum(target_d_c) == n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0abf8af3",
   "metadata": {},
   "source": [
    "We get the target degree sequence in the same way as before and note that is specifies degrees for `5` vertices when we have `6`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "62bbc8ce",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5-element Vector{Int64}:\n",
       " 2\n",
       " 2\n",
       " 1\n",
       " 1\n",
       " 1"
      ]
     },
     "execution_count": 19,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "target_d_s = degree_sequence(target_d_c)\n",
    "ds = deepcopy(target_d_s)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0c3ebd09",
   "metadata": {},
   "source": [
    "We simply use this line to append zeros to the target degree sequence."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "cc87600a",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "6-element Vector{Int64}:\n",
       " 2\n",
       " 2\n",
       " 1\n",
       " 1\n",
       " 1\n",
       " 0"
      ]
     },
     "execution_count": 20,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "length(ds) != n && append!(ds, zeros(n - length(ds)))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c02b7425",
   "metadata": {},
   "source": [
    "The new target degree sequence now contains enough information."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 21,
   "id": "f351d508",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "true"
      ]
     },
     "execution_count": 21,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "length(ds) == n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d51c49f9",
   "metadata": {},
   "source": [
    "Note that combining this with example 1.2.1. requires to run `target_d_d = target_d_c ./ sum(target_d_c)` to build the target degree distribution to sample a new degree sequence from."
   ]
  }
 ],
 "metadata": {
  "@webio": {
   "lastCommId": null,
   "lastKernelId": null
  },
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
